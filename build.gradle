group 'com.github.ffalcinelli'
version '1.1'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'net.java.dev.jna:jna:4.2.2'
    compile 'net.java.dev.jna:jna-platform:4.2.2'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

check.dependsOn jacocoTestReport

test {
    systemProperty "jna.nosys", "true"
}

task deleteDocs(){
    fileTree(dir: "docs", exclude: ".nojekyll")
}

task copyJavadoc(dependsOn: deleteDocs, type: Copy) {
    from 'build/docs/javadoc'
    into 'docs'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            pom.project {
                name 'jdivert'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'A Java binding to WinDivert driver'
                url 'https://github.com/jgrossmann492/jdivert'

                scm {
                    connection 'https://github.com/jgrossmann492/jdivert.git'
                    developerConnection 'https://github.com/jgrossmann492/jdivert.git'
                    url 'https://github.com/jgrossmann492/jdivert.git'
                }

                licenses {
                    license {
                        name 'GNU Lesser General Public License 3'
                        url 'https://www.gnu.org/licenses/lgpl-3.0.html'
                    }
                }

                developers {
                    developer {
                        id 'ffalcinelli'
                        name 'Fabio Falcinelli'
                        email 'fabio.falcinelli@gmail.com'
                    }
					
					developer {
						id 'jgrossmann'
						name 'John Grossmann'
						email 'jgrossmann492@gmail.com'
					}
                }
            }
        }
    }
}